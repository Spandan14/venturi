Script         <- Statement*

Statement      <- DimStmt
                / GridStmt
                / WindowStmt
                / SetStmt
                / DensityStmt
                / ForceStmt
                / SolidStmt

DimStmt        <- 'dim' _ DimType _
DimType        <- '2D' / '3D'

GridStmt       <- 'grid' _ Int _ 'x' _ Int _
WindowStmt     <- 'window' _ Int _ 'x' _ Int _

SetStmt        <- 'set' _ Identifier _ '=' _ CellSelector _
CellSelector   <- 'cells' _ 'where' _ Expr

DensityStmt    <- 'density' _ Target _ '=' _ Number _
ForceStmt      <- 'force'   _ Target _ '=' _ Vector2 _
SolidStmt      <- 'solid'   _ Target _ '=' _ Boolean _

Target         <- 'all' / Identifier

Vector2        <- '(' _ Number _ ',' _ Number _ ')'

Expr           <- OrExpr
OrExpr         <- AndExpr ( _ 'or' _ AndExpr )*
AndExpr        <- CmpExpr ( _ 'and' _ CmpExpr )*

CmpExpr        <- RangeExpr / BinaryCmp / ParenExpr

BinaryCmp      <- Var _ CmpOp _ Int
                / Int _ CmpOp _ Var

RangeExpr      <- Int _ '<=' _ Var _ '<=' _ Int

ParenExpr      <- '(' _ Expr _ ')'

Var            <- 'i' / 'j'
CmpOp          <- '==' / '<=' / '<' / '>=' / '>'
Number         <- < '-'? [0-9]+ ('.' [0-9]+)? >
Int            <- < '-'? [0-9]+ >
Boolean        <- 'true' / 'false'
Identifier     <- < [a-zA-Z_][a-zA-Z0-9_]* >

_              <- [ \t\r\n]*
